language Calc

start File

type File
type DSLStatement
type DSLExpr {
  grouping = "(" ")",
}

prod File: File = (s:DSLStatement ";")*
-- prod File: File = e:DSLExpr

prod Assignment: DSLStatement = "let" ident:LIdent "=" val:DSLExpr
-- prod Print: DSLStatement = "print" "(" val:DSLExpr ")"

prod Num: DSLExpr = val:Float
prod Variable: DSLExpr = ident:LIdent
prod Add: DSLExpr = left:DSLExpr "+" right:DSLExpr
prod Sub: DSLExpr = left:DSLExpr "-" right:DSLExpr
prod Mul: DSLExpr = left:DSLExpr "*" right:DSLExpr
prod Div: DSLExpr = left:DSLExpr "/" right:DSLExpr

precedence {
  Mul Div;
  Add Sub;
}

-------------------------------------------------------------------

-- Tokens. These will not be covered in the tutorial, but if you're
-- curious the language fragments defining each of these tokens
-- can be found here:
-- https://github.com/miking-lang/miking/blob/develop/stdlib/parser/lexer.mc

-- Basic tokens
token String {
  repr = StringRepr {},
  constructor = StringTok,
  fragment = StringTokenParser,
}
token Integer {
  repr = UIntRepr {},
  constructor = UIntTok,
  fragment = UIntTokenParser,
}
token Float {
  repr = FloatRepr {},
  constructor = FloatTok,
  fragment = UFloatTokenParser,
}
token LIdent {
  repr = LIdentRepr {},
  constructor = LIdentTok,
  fragment = LIdentTokenParser,
  ty = String,
}
token UIdent {
  repr = UIdentRepr {},
  constructor = UIdentTok,
  fragment = UIdentTokenParser,
  ty = String,
}

-- Token types only used through literals
token {fragment = OperatorTokenParser,}
token {fragment = CommaTokenParser,}
token {fragment = SemiTokenParser,}
token {fragment = BracketTokenParser,}

-- Whitespace and comments
token {fragment = LineCommentParser,}
token {fragment = MultilineCommentParser,}
token {fragment = WhitespaceParser,}
